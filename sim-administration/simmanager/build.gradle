plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "idea"
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

dependencies {

    implementation project(":prime-modules")
    
    implementation project(":sim-administration:jersey-json-schema-validator")
    implementation project(":sim-administration:simcard-utils")
    implementation project(":sim-administration:es2plus4dropwizard")
    implementation project(":sim-administration:ostelco-dropwizard-utils")
    implementation project(":sim-administration:sm-dp-plus")

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-client:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation "com.google.guava:guava:$guavaVersion"

    // Jdbi3 plugins.
    // Ver. 3.2.1 as ver. 1.3.8 of dropwizard loads jdbi3-core ver. 3.2.1 etc.
    implementation "org.jdbi:jdbi3-kotlin:3.2.1"
    implementation "org.jdbi:jdbi3-kotlin-sqlobject:3.2.1"
    implementation "org.jdbi:jdbi3-postgres:3.2.1"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"
    implementation "org.apache.commons:commons-csv:1.6"

    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "org.testcontainers:postgresql:1.10.5"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += file('src/integration-test/kotlin')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

build.dependsOn integration

apply from: '../../gradle/jacoco.gradle'

idea {
    module {
        testSourceDirs += file('src/integration-test/kotlin')
    }
}