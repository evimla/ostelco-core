plugins {
  id "application"
  id "jacoco"
  id "com.github.johnrengelman.shadow" version "2.0.1"
  id "org.jetbrains.kotlin.jvm" version "1.2.21"
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "io.dropwizard:dropwizard-core:1.2.2"
  implementation 'com.google.firebase:firebase-admin:5.9.0'
  testImplementation "io.dropwizard:dropwizard-testing:1.2.2"
  testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.2.21'
  testRuntime 'org.hamcrest:hamcrest-all:1.3'
}

shadowJar {
  mainClassName = 'com.telenordigital.ostelco.auth.AuthServerApplicationKt'
  mergeServiceFiles()
  classifier = "uber"
  version = null
}

task pack(type: Zip, dependsOn: 'shadowJar') {
  from('config/') {
    into(project.name + '/config')
  }
  from('script/') {
    into(project.name + '/script')
  }
  from('build/libs/auth-server-uber.jar') {
    into(project.name + '/build/libs/')
  }
  from('Dockerfile') {
    into(project.name)
  }
  archiveName 'auth-server.zip'
  destinationDir(file('build/deploy/'))
}

jacocoTestReport {
  group = "Reporting"
  description = "Generate Jacoco coverage reports after running tests."
  additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport