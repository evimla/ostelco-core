plugins {
    id "application"
    id "jacoco"
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id 'net.ltgt.apt' version '0.10'
    id "idea"
}

sourceSets {
  test {
    java.srcDirs = ['src/test/java']
  }

  integration {
    java.srcDirs = ['src/test/java', 'src/integration-tests/java']
    resources.srcDir 'src/integration-tests/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

dependencies {
    implementation project(':ocs-api')
    implementation project(':ostelco-lib')
    implementation 'io.dropwizard:dropwizard-core:1.1.4'
    implementation 'com.google.firebase:firebase-admin:5.3.1'
    implementation 'com.lmax:disruptor:3.3.7'
    implementation 'com.google.guava:guava:21.0'

    testImplementation 'io.dropwizard:dropwizard-testing:1.1.4'
    testCompile 'org.mockito:mockito-core:2.10.0'

    compileOnly 'org.projectlombok:lombok:1.16.20'
    apt 'org.projectlombok:lombok:1.16.20'
}

configurations {
  integrationImplementation.extendsFrom testImplementation
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {

  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

shadowJar {
    mainClassName = 'com.telenordigital.prime.PrimeApplication'
    mergeServiceFiles()
    classifier = "uber"
    version = null
}

task pack(type: Zip, dependsOn: 'shadowJar') {
    from ('config/') {
        into('config')
    }
    from ('files/') {
        into('files')
    }
    from ('build/libs/') {
        into('build/libs/')
    }
    from 'Dockerfile'
    archiveName 'prime.zip'
    destinationDir(file('build/deploy/'))
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

idea {
    module {
        testSourceDirs += file('src/integration-tests/java')
    }
}