plugins {
    id "java"
    id "maven"
    id "project-report"
    id "com.github.ben-manes.versions" version "0.17.0"
}

allprojects {

    group = 'org.ostelco'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://repository.jboss.org/nexus/content/repositories/releases/"
        }

    }
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

task _unpackOcsgw(type: org.gradle.api.tasks.Copy, dependsOn: ':ocsgw:pack') {
    from zipTree('ocsgw/build/deploy/ocsgw.zip') into 'build/tmp/unpack/ocsgw'
}

task _unpackPrime(type: org.gradle.api.tasks.Copy, dependsOn: ':prime:pack') {
    from zipTree('prime/build/deploy/prime.zip') into 'build/tmp/unpack/prime'
}

task pack(type: Zip, dependsOn: ['_unpackOcsgw', '_unpackPrime']) {
    from 'docker-compose.yaml'
    from 'build/tmp/unpack'
    archiveName 'ostelco-core.zip'
    destinationDir(file('build/deploy/'))
}