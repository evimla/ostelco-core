// these are needed only in top-level module
plugins {
  id "java"
  id "project-report"
  id "com.github.ben-manes.versions" version "0.21.0"
  id "jacoco"
}

allprojects {
  apply plugin: 'jacoco'

  group = 'org.ostelco'
  version = '1.0.0-SNAPSHOT'

  repositories {
    mavenCentral()
    jcenter()
    maven { url = "http://repository.jboss.org/nexus/content/repositories/releases/" }
    maven { url = "https://maven.repository.redhat.com/ga/" }
    maven { url = "http://clojars.org/repo/" }
    maven { url "https://jitpack.io" }
  }

  jacoco {
    toolVersion = "0.8.2"
  }
}

subprojects {
  sourceCompatibility = 11
  targetCompatibility = 11
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
  ext {
    assertJVersion = "3.12.1"
    arrowVersion = "0.8.2"
    beamVersion = "2.11.0"
    cxfVersion = "3.3.1"
    dockerComposeJunitRuleVersion = "0.35.0"
    dropwizardVersion = "1.3.9"
    metricsVersion = "4.0.0"
    firebaseVersion = "6.7.0"
    googleCloudVersion = "1.63.0"
    grpcVersion = "1.19.0"
    guavaVersion = "27.0.1-jre"
    jacksonVersion = "2.9.8"
    javaxActivationVersion = "1.1.1"
    // Keeping it version 1.16.1 to be consistent with grpc via PubSub client lib
    // Keeping it version 1.16.1 to be consistent with netty via Firebase lib
    jaxbVersion = "2.3.1"
    jjwtVersion = "0.9.1"
    junit5Version = "5.4.0"
    kotlinVersion = "1.3.21"
    kotlinXCoroutinesVersion = "1.1.1"
    mockitoVersion = "2.24.5"
    neo4jDriverVersion="1.7.2"
    neo4jVersion="3.5.3"
    slf4jVersion="1.7.26"
    stripeVersion = "7.25.0"
    swaggerVersion = "2.0.7"
    tinkVersion = "1.2.2"
    zxingVersion = "3.3.3"
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'redhat'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}