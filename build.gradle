// these are needed only in top-level module
plugins {
  id "java"
  id "project-report"
  id "com.github.ben-manes.versions" version "0.20.0"
  id "jacoco"
}

allprojects {
  apply plugin: 'jacoco'

  group = 'org.ostelco'
  version = '1.0.0-SNAPSHOT'

  repositories {
    mavenCentral()
    jcenter()
    maven { url = "https://repository.jboss.org/nexus/content/repositories/releases/" }
    maven { url = "https://maven.repository.redhat.com/ga/" }
    maven { url = "http://clojars.org/repo/" }
  }

  jacoco {
    toolVersion = "0.8.2"
  }
}

subprojects {
  sourceCompatibility = 11
  targetCompatibility = 11
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
  ext {
    kotlinVersion = "1.3.0"
    dropwizardVersion = "1.3.7"
    kotlinXCoroutinesVersion = "1.0.0"
    googleCloudVersion = "1.50.0"
    jacksonVersion = "2.9.7"
    stripeVersion = "7.1.0"
    guavaVersion = "27.0-jre"
    junit5Version = "5.3.1"
    assertJVersion = "3.11.1"
    mockitoVersion = "2.23.0"
    firebaseVersion = "6.5.0"
    beamVersion = "2.8.0"
    neo4jVersion="3.4.9"
    neo4jDriverVersion="1.7.0"
    // Keeping it version 1.16.1 to be consistent with grpc via PubSub client lib
    // Keeping it version 1.16.1 to be consistent with netty via Firebase lib
    grpcVersion = "1.16.1"
    jaxbVersion = "2.3.0"
    javaxActivationVersion = "1.1.1"
    arrowVersion = "0.7.3"
  }
}

task pack(dependsOn: ['packDev', 'packProd'])

task packDev(type: Zip, dependsOn: [':ocsgw:packDev', ':auth-server:pack']) {
  from zipTree('ocsgw/build/deploy/dev/ocsgw.zip')
  from zipTree('auth-server/build/deploy/auth-server.zip')
  from 'docker-compose.yaml'
  from 'docker-compose.dev.yaml'
  rename 'docker-compose.dev.yaml','docker-compose.override.yaml'
  archiveName = 'ostelco-core-dev.zip'
  destinationDir = file('build/deploy/')
}

task packProd(type: Zip, dependsOn: [':ocsgw:packProd', ':auth-server:pack']) {
  from zipTree('ocsgw/build/deploy/prod/ocsgw.zip')
  from zipTree('auth-server/build/deploy/auth-server.zip')
  from 'docker-compose.yaml'
  from 'docker-compose.prod.yaml'
  rename 'docker-compose.prod.yaml','docker-compose.override.yaml'
  archiveName = 'ostelco-core-prod.zip'
  destinationDir = file('build/deploy/')
}


//dependencyUpdates.resolutionStrategy {
//  componentSelection { rules ->
//    rules.all { ComponentSelection selection ->
//      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'redhat'].any { qualifier ->
//        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
//      }
//      if (rejected) {
//        selection.reject('Release candidate')
//      }
//    }
//  }
//}