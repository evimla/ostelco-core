// these are needed only in top-level module
plugins {
  id "java"
  id "project-report"
  id "com.github.ben-manes.versions" version "0.20.0"
  id "jacoco"
}

allprojects {
  apply plugin: 'jacoco'

  group = 'org.ostelco'
  version = '1.0.0-SNAPSHOT'

  repositories {
    mavenCentral()
    jcenter()
    maven { url = "http://repository.jboss.org/nexus/content/repositories/releases/" }
    maven { url = "https://maven.repository.redhat.com/ga/" }
    maven { url = "http://clojars.org/repo/" }
    maven { url "https://jitpack.io" }
  }

  jacoco {
    toolVersion = "0.8.2"
  }
}

subprojects {
  sourceCompatibility = 11
  targetCompatibility = 11
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
  ext {
    assertJVersion = "3.11.1"
    arrowVersion = "0.8.2"
    beamVersion = "2.9.0"
    dropwizardVersion = "1.3.8"
    firebaseVersion = "6.7.0"
    googleCloudVersion = "1.60.0"
    grpcVersion = "1.18.0"
    guavaVersion = "27.0.1-jre"
    jacksonVersion = "2.9.8"
    javaxActivationVersion = "1.1.1"
    // Keeping it version 1.16.1 to be consistent with grpc via PubSub client lib
    // Keeping it version 1.16.1 to be consistent with netty via Firebase lib
    jaxbVersion = "2.3.1"
    junit5Version = "5.3.2"
    kotlinVersion = "1.3.11"
    kotlinXCoroutinesVersion = "1.1.0"
    mockitoVersion = "2.23.4"
    neo4jDriverVersion="1.7.2"
    neo4jVersion="3.5.1"
    stripeVersion = "7.17.0"
    swaggerVersion = "2.0.6"
    tinkVersion = "1.2.2"
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'redhat'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}