plugins {
  id "org.jetbrains.kotlin.jvm"
  id "java-library"
  id "idea"
}

sourceSets {
  integration {
    java.srcDirs = ['src/integration-tests/kotlin']
    resources.srcDir 'src/integration-tests/resources'
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

dependencies {
  implementation project(":prime-modules")
  implementation project(":data-store")

  implementation "com.stripe:stripe-java:$stripeVersion"

  implementation "com.google.cloud:google-cloud-pubsub:$googleCloudVersion"
  implementation "com.google.cloud:google-cloud-datastore:$googleCloudVersion"

  testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

  integrationImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
  integrationImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

configurations {
  integration
  integrationImplementation.extendsFrom implementation
  integrationImplementation.extendsFrom runtime
  integrationImplementation.extendsFrom runtimeOnly
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
}

build.dependsOn integration

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

apply from: '../gradle/jacoco.gradle'

idea {
  module {
    testSourceDirs += file('src/integration-tests/kotlin')
  }
}